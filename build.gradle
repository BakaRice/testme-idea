buildscript {
  repositories {
    maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
  }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.7"
    id 'jacoco'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
//    id 'com.github.kt3k.coveralls' version '2.6.3'
}
println "*************** Running tests for ideaVersion: $ideaVersion *****************************"
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'

def isRunInCI=Boolean.valueOf(isRunInCI)
def enableIdeaGroovyPlugin=Boolean.valueOf(System.getProperty('enableIdeaGroovyPlugin','true'))
def spockVersion = ideaVersion.startsWith("14.")? "1.0-groovy-2.3" : "1.0-groovy-2.4"

jacoco {
    toolVersion = "0.7.6.201602180812"
}

repositories {
    mavenCentral()
}

sourceCompatibility = jvmTargetVersion
targetCompatibility = jvmTargetVersion

testSets {
    integrationTest
}
check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}
test {
    afterTest { desc, result ->
        println "Executing test [${desc.className}].${desc.name} with result: ${result.resultType}"
    }
    testLogging {
        exceptionFormat = 'full'
        events "failed"
        showStandardStreams = true
    }
    reports.html.enabled = !isRunInCI
}
integrationTest {
    afterTest { desc, result ->
        println "Executing test [${desc.className}].${desc.name} with result: ${result.resultType}"
    }
    reports.html.enabled = !isRunInCI
}

tasks.build.dependsOn(jacocoTestReport)

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    if (!javaHome) {
        javaHome =  System.env.JAVA_HOME
    }
    options.bootClasspath = "$javaHome/jre/lib/rt.jar"
}
def mandatoryPlugins = ['junit','properties']

intellij {
    version ideaVersion
    type ideaType
    pluginName 'TestMe'
    downloadSources !isRunInCI
    sameSinceUntilBuild false
    updateSinceUntilBuild false
    plugins = enableIdeaGroovyPlugin ? (mandatoryPlugins + 'Groovy') : mandatoryPlugins

    publish {
        username ideaPublishUsername
        password ideaPublishPassword
        channel ideaPublishChannel
    }
}
idea {
    project {
        jdkName         = jvmTargetVersion
        languageLevel   = jvmTargetVersion
        vcs = 'Git'
    }
}

tasks.withType(Test) { systemProperty 'enableIdeaGroovyPlugin', enableIdeaGroovyPlugin }

jacocoTestReport {
    reports {
        xml.enabled false //true - if coveralls enabled
        csv.enabled false
        html{
            enabled !isRunInCI
            destination "${buildDir}/reports/jacoco"
        }
    }
    executionData(test, integrationTest)
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['com/weirddev/testme/intellij/ui/**'])
        })
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile("org.spockframework:spock-core:$spockVersion"){
        exclude group: 'org.codehaus.groovy'
    }
}
